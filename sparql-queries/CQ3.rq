prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
prefix rdfs:    <http://www.w3.org/2000/01/rdf-schema#> 
prefix xsd:     <http://www.w3.org/2001/XMLSchema#> 
prefix schema:  <http://schema.org/> 
prefix owl:     <http://www.w3.org/2002/07/owl#> 
prefix skos:    <http://www.w3.org/2004/02/skos/core#> 
prefix oa:      <http://www.w3.org/ns/oa#> 
prefix ncbi:    <http://identifiers.org/taxonomy/> 
prefix dct:     <http://purl.org/dc/terms/> 
prefix frbr:    <http://purl.org/vocab/frbr/core#> 
prefix fabio:   <http://purl.org/spar/fabio/> 
prefix obo:     <http://purl.obolibrary.org/obo/> 
prefix bibo: <http://purl.org/ontology/bibo/> 
prefix d2kab:   <http://ns.inria.fr/d2kab/> 
prefix dc: <http://purl.org/dc/terms/> 

SELECT distinct ?paper ?Title ?GeneName ?WTOtrait
FROM  <http://ns.inria.fr/d2kab/wheatgenomicsslkg>
FROM  <http://ns.inria.fr/d2kab/ontology/wto/v3>
WHERE {
   
  ?a1 a oa:Annotation; 
     oa:hasTarget [ oa:hasSource ?source1 ];
     oa:hasBody  [ a d2kab:Gene; skos:prefLabel ?GeneName ].
  
  ?source1 frbr:partOf+ ?paper . 

  ?a2 a oa:Annotation; 
      oa:hasTarget [ oa:hasSource ?source2 ]; 
      oa:hasBody ?body.
   
  ?source2 frbr:partOf+ ?paper .
  
   {
   ?body rdf:type ?class; skos:prefLabel ?WTOtrait .
   ?class rdfs:subClassOf* <http://opendata.inrae.fr/wto/0000340> .
   }
    UNION 
    {
    ?body rdfs:subClassOf* <http://opendata.inrae.fr/wto/0000340>; 
          rdfs:label ?WTOtrait .
    }
    UNION
    {
      ?body skos:broader* ?concept ; skos:prefLabel ?WTOtrait .
      ?concept rdf:type [ rdfs:subClassOf* <http://opendata.inrae.fr/wto/0000340> ].     
    }
   ?paper a fabio:ResearchPaper ; dct:title ?titleURI .
   ?titleURI rdf:value ?Title.
}
